(mod
  (
    MOD_HASH
    RATE_AMOUNT
    INTERVAL_TIME
    EARNINGS_CAP
    CREDIT
    LAST_HEIGHT
    INNER_PUZZLE
    Truths
    current_height
    inner_solution
  )

  (include condition_codes.clib)
  (include curry_and_treehash.clib)
  (include sha256tree.clib)
  (include singleton_truths.clib)
  (include singleton_finder.clib)
  (include utility_functions.clib) ; or, assert, and

  (defun-inline coin_amount (condition) (f (r (r condition))))
  (defun-inline puzzle_hash (condition) (f (r condition)))

  (defun-inline >= (a b)
    (or (> a b)
        (= a b)
    )
  )

  (defun min (a b)
    (if (> a b)
      b
      a
    )
  )

  (defun max (a b)
    (if (> a b)
      a
      b
    )
  )

  (defun-inline morph_singleton_condition
    (
      singleton_condition
      MOD_HASH
      RATE_AMOUNT
      INTERVAL_TIME
      EARNINGS_CAP
      current_height
      new_credit
    )

    (c
      CREATE_COIN
      (c
        (puzzle_hash_of_curried_function MOD_HASH
          (puzzle_hash singleton_condition)
          (sha256 1 current_height)
          (sha256 1 new_credit)
          (sha256 1 EARNINGS_CAP)
          (sha256 1 INTERVAL_TIME)
          (sha256 1 RATE_AMOUNT)
          (sha256 1 MOD_HASH)
        )
        (r (r singleton_condition))
      )
    )
  )


  (defun-inline assert_rate (EARNINGS_CAP possible_withdrawal_amount withdrawal_amount current_height)
    (assert
      (>= EARNINGS_CAP withdrawal_amount)
      (>= possible_withdrawal_amount withdrawal_amount)
      (list ASSERT_HEIGHT_ABSOLUTE current_height)
    )
  )

  (defun-inline calculate_possible_withdrawal (RATE_AMOUNT INTERVAL_TIME CREDIT LAST_HEIGHT current_height)
    (+ (* (max 0 (- current_height LAST_HEIGHT)) (/ RATE_AMOUNT INTERVAL_TIME)) CREDIT)
  )

  (defun generate_final_conditions
    (
      singleton_condition
      remaining_conditions
      withdrawal_amount
      possible_withdrawal_amount
      MOD_HASH
      RATE_AMOUNT
      INTERVAL_TIME
      EARNINGS_CAP
      current_height
    )

    (c
      (assert_rate EARNINGS_CAP possible_withdrawal_amount withdrawal_amount current_height)
      (c
        (morph_singleton_condition
          singleton_condition
          MOD_HASH
          RATE_AMOUNT
          INTERVAL_TIME
          EARNINGS_CAP
          current_height
          (- (min possible_withdrawal_amount EARNINGS_CAP) withdrawal_amount)
        )
        remaining_conditions
      )
    )
  )

  (defun found_singleton
    (
      singleton_condition
      remaining_conditions
      (
        MOD_HASH
        RATE_AMOUNT
        INTERVAL_TIME
        EARNINGS_CAP
        CREDIT
        LAST_HEIGHT
        current_height
        my_amount
      )
    )

    (generate_final_conditions
      singleton_condition
      remaining_conditions
      (max (- my_amount (coin_amount singleton_condition)) 0)
      (calculate_possible_withdrawal RATE_AMOUNT INTERVAL_TIME CREDIT LAST_HEIGHT current_height)
      MOD_HASH
      RATE_AMOUNT
      INTERVAL_TIME
      EARNINGS_CAP
      (max current_height LAST_HEIGHT)
    )
  )

  ; main
  (search_for_singleton
    (a INNER_PUZZLE (c Truths inner_solution))
    found_singleton
    (list
      MOD_HASH
      RATE_AMOUNT
      INTERVAL_TIME
      EARNINGS_CAP
      CREDIT
      LAST_HEIGHT
      current_height
      (my_amount_truth Truths)
    )
  )
)
